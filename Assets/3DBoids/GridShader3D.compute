#define blockSize 256

struct Boid {
  float3 pos;
  float3 vel;
  float pad0;
  float pad1;
};

StructuredBuffer<Boid> boids;
RWStructuredBuffer<Boid> boidsOut;
RWStructuredBuffer<uint2> gridBuffer;
RWStructuredBuffer<uint> gridOffsetBuffer;
StructuredBuffer<uint> gridOffsetBufferIn;
RWStructuredBuffer<uint> gridSumsBuffer;
StructuredBuffer<uint> gridSumsBufferIn;

float gridCellSize;
uint gridDimX;
uint gridDimY;
uint gridDimZ;
uint gridTotalCells;
uint buckets;
uint numBoids;
uint d;

uint getGridID(Boid boid) {
  int boidX = floor(boid.pos.x / gridCellSize + gridDimX / 2);
  int boidY = floor(boid.pos.y / gridCellSize + gridDimY / 2);
  int boidZ = floor(boid.pos.z / gridCellSize + gridDimZ / 2);
  return (gridDimY * gridDimX * boidZ) + (gridDimX * boidY) + boidX;
}

#pragma kernel UpdateGrid // ID 0
[numthreads(blockSize, 1, 1)]
void UpdateGrid(uint3 id : SV_DispatchThreadID) {
  if (id.x >= numBoids) {
    return;
  }

  int gridID = getGridID(boids[id.x]);
  gridBuffer[id.x].x = gridID;
  InterlockedAdd(gridOffsetBuffer[gridID], 1, gridBuffer[id.x].y);
}

#pragma kernel ClearGrid // ID 1
[numthreads(blockSize, 1, 1)]
void ClearGrid(uint3 id : SV_DispatchThreadID) {
  if (id.x >= gridTotalCells) {
    return;
  }

  gridOffsetBuffer[id.x] = 0;
}

groupshared int temp[blockSize * 2];
#pragma kernel PrefixSum // ID 2
[numthreads(blockSize, 1, 1)]
void PrefixSum(uint globalID : SV_DispatchThreadID, uint localID : SV_GROUPTHREADID, uint groupID : SV_GROUPID) {

  int pout = 0, pin = 1;
  temp[localID] = gridOffsetBufferIn[globalID];
  GroupMemoryBarrierWithGroupSync();

  [unroll]
  for (uint offset = 1; offset < blockSize; offset *= 2) {
    pout = 1 - pout; // swap double buffer indices
    pin = 1 - pout;
    if (localID >= offset) {
      temp[pout * blockSize + localID] = temp[pin * blockSize + localID] + temp[pin * blockSize + localID - offset];
    } else {
      temp[pout * blockSize + localID] = temp[pin * blockSize + localID];
    }
    GroupMemoryBarrierWithGroupSync();
  }

  gridOffsetBuffer[globalID] = temp[pout * blockSize + localID];
  if (localID == 0) {
    gridSumsBuffer[groupID] = temp[pout * blockSize + blockSize - 1];
  }
}

#pragma kernel RearrangeBoids // ID 3
[numthreads(blockSize, 1, 1)]
void RearrangeBoids(uint3 id : SV_DispatchThreadID) {
  if (id.x >= numBoids) {
    return;
  }
  
  uint gridID = gridBuffer[id.x].x;
  uint cellOffset = gridBuffer[id.x].y;
  uint index = gridOffsetBuffer[gridID] - 1 - cellOffset;
  boidsOut[index] = boids[id.x];
}

#pragma kernel SumBuckets // ID 4
[numthreads(blockSize, 1, 1)]
void SumBuckets(uint3 id : SV_DISPATCHTHREADID) {
  if (id.x >= buckets) {
    return;
  }

  uint k = id.x;

  if (k < d) {
    gridSumsBuffer[k] = gridSumsBufferIn[k];
  } else {
    gridSumsBuffer[k] = gridSumsBufferIn[k] + gridSumsBufferIn[k - d];
  }
}

#pragma kernel AddSums // ID 5
[numthreads(blockSize, 1, 1)]
void AddSums(uint globalID : SV_DISPATCHTHREADID, uint groupID : SV_GROUPID) {
  gridOffsetBuffer[globalID] += gridSumsBufferIn[groupID - 1];
}